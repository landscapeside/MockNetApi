if (isLibModule.toBoolean()) {
  apply plugin: 'com.android.library'
} else {
  apply plugin: 'com.android.application'
}
apply plugin: 'com.github.dcendents.android-maven'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion

  defaultConfig {
    if (!isLibModule.toBoolean()) {
      applicationId "com.landscape.mocknetapi"
    }
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode rootProject.ext.appVersion
    versionName rootProject.ext.appVersionName

    if (!isLibModule.toBoolean()) {
      testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  defaultPublishConfig "mockModeRelease"

  flavorDimensions "${rootProject.ext.appVersion}"

  productFlavors{
    mockMode{

    }
    internal{}

  }


  sourceSets {
    main {
      if (isLibModule.toBoolean()) {
        manifest.srcFile 'src/libModule/AndroidManifest.xml'
      } else {
        manifest.srcFile 'src/main/AndroidManifest.xml'
      }
    }
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.javaDirectories
}

artifacts {
  archives sourcesJar
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  if (isLibModule.toBoolean()) {
  } else {
    androidTestApi('com.android.support.test.espresso:espresso-core:2.2.2', {
      exclude group: 'com.android.support', module: 'support-annotations'
    })
  }
  implementation "com.android.support:appcompat-v7:$SupportVersion"
  testImplementation 'junit:junit:4.12'
  implementation 'com.google.code.gson:gson:2.6.2'
  implementation 'com.github.orhanobut:logger:1.12'

  implementation "io.reactivex.rxjava2:rxandroid:$rxjavaAndroidVersion"
  implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"

  implementation "com.squareup.retrofit2:converter-gson:$converterGsonVersion"
  implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitRxAdatperVersion"
}

group='com.github.landscapeside.MockNetApi'
version = rootProject.ext.appVersionName
if (isLibModule.toBoolean()) {
  if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->
      if (variant.name.toLowerCase().contains("debug")) {
        return
      }

      def bundleTask = tasks["bundle${variant.name.capitalize()}Aar"]

      artifacts {
        archives(bundleTask.archivePath) {
          classifier variant.flavorName
          builtBy bundleTask
          name = project.name
        }
      }
    }
  }
}
